plugins {
    id 'application'
    id 'java'
    id 'com.google.protobuf' version '0.9.1'
    id 'idea'
}

group = 'org.example'
version = '1.0-SNAPSHOT'

repositories {
    maven { // The google mirror is less flaky than mavenCentral()
        url "https://maven-central.storage-download.googleapis.com/maven2/" }
    mavenCentral()
    mavenLocal()
}

dependencies {
    implementation 'io.grpc:grpc-protobuf:1.57.0'
    implementation 'io.grpc:grpc-stub:1.57.0'
    implementation 'io.grpc:grpc-netty:1.57.0'
    implementation 'javax.annotation:javax.annotation-api:1.3.2'
    implementation 'org.postgresql:postgresql:42.6.0'

    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    implementation group: 'org.jsoup', name: 'jsoup', version: '1.16.1'
    implementation group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-xml', version: '2.15.2'
    implementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: '2.15.2'
    implementation group: 'org.apache.httpcomponents.client5', name: 'httpclient5', version: '5.2.1'
    compileOnly 'org.projectlombok:lombok:1.18.28'
    annotationProcessor 'org.projectlombok:lombok:1.18.28'
}

application {
    mainClass = 'org.example.Main'
}

jar {
    dependsOn generateProto
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    manifest {
        attributes('Main-Class': 'org.example.Main')
    }

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

protobuf{
    protoc {artifact = "com.google.protobuf:protoc:3.13.0"}
    plugins{
        grpc {artifact = "io.grpc:protoc-gen-grpc-java:1.31.1"}
    }
    generateProtoTasks{
        all()*.plugins {grpc {}}
    }
    // default proto plugin generate stub in build folder
    // change the stub generate folder
    //generatedFilesBaseDir = "$projectDir/src/generated"
}

sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}

task runApp(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath

    mainClass = 'org.example.Main'

    // arguments to pass to the application
    args 'afisha', 'index'
}

test {
    useJUnitPlatform()
}
java.sourceCompatibility = JavaVersion.VERSION_17
java.targetCompatibility = JavaVersion.VERSION_17